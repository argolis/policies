

bundle agent proftpd {

    vars:
        "packages" slist =>  { "proftpd-basic",
                               "proftpd-mod-mysql",
                               "mysql-server"
                             };


    packages:
        "$(packages)"
            policy => "present";


    services:
        "proftpd"
            service_policy => "start";
        
        "mysql"
            service_policy => "start";


    files:
        "/etc/proftpd/."
            comment => "ProFTPD config directory",
            create  => "true",
            perms   => m("750");

        "/etc/proftpd/proftpd.conf"
            comment       => "ProFTPD main config file",
            create        => "true",
            perms         => m("640"),
            edit_template => "$(sys.inputdir)/files/etc/proftpd/proftpd.conf";

        "/etc/proftpd/modules.conf"
            comment       => "ProFTPD modules config file",
            create        => "true",
            perms         => m("640"),
            edit_template => "$(sys.inputdir)/files/etc/proftpd/modules.conf";

        "/var/log/proftpd/."
            comment => "ProFTPD log directory",
            create  => "true",
            perms   => m("755");

        "/var/log/mysql/."
            comment => "MySQL log directory",
            create  => "true",
            perms   => m("755");

        "/usr/local/bin/create_proftpd_database"
            comment       => "Shell script to create ProFTPD database",
            create        => "true",
            perms         => mog("0754", "root", "staff"),
            edit_template => "$(sys.inputdir)/files/usr/local/bin/create_proftpd_database";

    commands:
        "/usr/local/bin/create_proftpd_database";

}

# vim: ft=cf3 foldlevel=99
