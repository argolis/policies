

bundle agent proftpd {

    vars:
        ftp_sandara_org::
            "packages" slist =>  { "proftpd-basic",
                                   "proftpd-mod-mysql",
                                   "mysql-server"
                                 };


    packages:
        ftp_sandara_org::
            "$(packages)"
                policy => "present";


    services:
        ftp_sandara_org::
            "proftpd"
                service_policy => "start";
            "mysql"
                service_policy => "start";


    files:
        ftp_sandara_org::
            "/etc/proftpd/."
                comment => "ProFTPD config directory",
                create  => "true",
                perms   => m("750");

            "/etc/proftpd/proftpd.conf"
                comment       => "ProFTPD main config file",
                create        => "true",
                perms         => m("640"),
                edit_line     => expand_template("$(sys.inputdir)/files/etc/proftpd/proftpd.conf"),
                edit_defaults => empty;

            "/etc/proftpd/modules.conf"
                comment       => "ProFTPD modules config file",
                create        => "true",
                perms         => m("640"),
                edit_line     => expand_template("$(sys.inputdir)/files/etc/proftpd/modules.conf"),
                edit_defaults => empty;

            "/var/log/proftpd/."
                comment => "ProFTPD log directory",
                create  => "true",
                perms   => m("755");

            "/var/log/mysql/."
                comment => "MySQL log directory",
                create  => "true",
                perms   => m("755");

            "/usr/local/bin/create_proftpd_database"
                comment       => "Shell script to create ProFTPD database",
                create        => "true",
                perms         => mog("0754", "root", "staff"),
                edit_line     => expand_template("$(sys.inputdir)/files/usr/local/bin/create_proftpd_database"),
                edit_defaults => empty;

    commands:
        ftp_sandara_org::
            "/usr/local/bin/create_proftpd_database";

}

# vim: ft=cf3 foldlevel=99
